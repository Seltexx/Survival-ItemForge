plugins {
    id 'java'
    id "xyz.jpenilla.run-paper" version "2.3.1"
    id("com.gradleup.shadow") version "9.3.0"
    id "io.papermc.paperweight.userdev" version "2.0.0-beta.19"
}

group = 'de.relaxogames'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
    gradlePluginPortal()
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://jitpack.io" }
}

java {
    toolchain { languageVersion = JavaLanguageVersion.of(21) }
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

dependencies {
    paperweight.paperDevBundle("1.21.10-R0.1-SNAPSHOT")

    compileOnly('com.github.koca2000:NoteBlockAPI:1.6.2')

    implementation 'de.relaxogames:LingoAPI:inDev-1.7-RC02'
    shadow        ("de.relaxogames:LingoAPI:inDev-1.7-RC02")

    implementation 'com.jeff-media:custom-block-data:2.2.4'
    shadow        'com.jeff-media:custom-block-data:2.2.4'
    compileOnly 'net.dmulloy2:ProtocolLib:5.4.0'
}

configurations {
    shadow
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(21)
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') { expand props }
}

tasks {
    runServer { minecraftVersion("1.21") }
}
shadowJar {
    configurations = [project.configurations.shadow]
    // Service-Dateien unter META-INF/services werden automatisch zusammengeführt
    mergeServiceFiles()

    // Problematische META-INF-Dateien von Signaturen und Zertifikaten ausschließen
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'

    // Optional: Manifest selbst definieren, um Konflikte zu vermeiden
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
}


